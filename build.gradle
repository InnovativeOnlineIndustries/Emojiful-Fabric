plugins {
    id "fabric-loom" version "0.4-SNAPSHOT"
    id "java"
    id "idea"
    //id "com.wynprice.cursemaven" version "2.1.1"
}

//version = "${minecraft_version}-" + "${version}"
version = "${version}+fabric-${minecraft_version}"
group = "${group}"
archivesBaseName = "${archive}"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    accessWidener "src/main/resources/emojiful.accesswidener"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    minecraft("com.mojang:minecraft:${minecraft_version}")
    mappings("net.fabricmc:yarn:${minecraft_version}+build.${mappings_build}:v2")
    modImplementation("net.fabricmc:fabric-loader:${loader_version}")

    include modApi("com.esotericsoftware.yamlbeans:yamlbeans:1.13")

    modImplementation(fabricApi.module("fabric-textures-v0", "0.17.0+build.386-1.16.1"))
    modImplementation(fabricApi.module("fabric-api-base", "0.17.0+build.386-1.16.1"))
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${title}",
                "Specification-Vendor"    : "${team}",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${project.version}",
                "Implementation-Vendor"   : "${team}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "ModSide": "BOTH",
        ],)
    }
}